title: StallLevels
---

<<declare $beat_parts = 0>>           // 0..3 — сколько уроков получил
<<declare $seen_visits = 1>>          // счётчик подходов (для петель)
<<declare $has_looper = false>>       // у игрока есть лупер (сломанный)
<<declare $has_camera = false>>       // игрок заранее мог иметь камеру
<<declare $has_pizza = false>>        // игрок заранее мог иметь пиццу
<<declare $looper_given = false>>     // лупер принят NPC (миссия 1 завершена)
<<declare $camera_given = false>>     // камера принята NPC (миссия 2 завершена)
<<declare $pizza_given = false>>      // пицца принята NPC (миссия 3 завершена)
<<declare $stage = 0>>                // 1 - ждем лупера, 2 - ждем камеру, 3 - ждем пиццу, 4 - экзамен

//Реплики Толстого — звуки бита, отражающие три основных настроения ("Б!" — радость, довольство; "ПФ." —  злость, недовольство; "ТС..." весеслье, насмешку). Повторение означает усиленную версию эмоции. Возможны комбинации.

=== 
title: Start
---
<<jump Approach_Stall>>

=== 
title: Approach_Stall
---

// Если игрок вернулся за уроком
<<if $seen_visits >= 3 and $stage <= 3 and $stage > 0 >>
    Длинный: Э, вернулся? Вот это правильный подход, малой — так учатся.  
    Толстый: Б-Б!

    -> Я готов учиться. //Принять
        Длинный: Да ладно? Ну, покажешь, че получается. Но сначала потворим все... Где мы там остановились?
        <<jump Give_Lesson>>
    -> Я просто мимо проходил.//Отказать
        Длинный: Ну, у нас тут дело серьёзное — либо урок, либо отвали.
        Толстый: ПФ. ПФ.
        <<jump End_Scene>>
    ->  Чем вы тут вообще занимаетесь? //Уклониться
        Длинный: Как чем? Готовимся к батлу, конечно! Это самый чумофигенский батл в мире, сечешь?
        Толстый: Б! ТС...
        Длинный: Че, готов учиться-то?
        ->Да
        <<jump Give_Lesson>>
        ->Нет
         Длинный: Ну, у нас тут дело серьёзное — либо урок, либо отвали.
         Толстый: ПФ. ПФ.
         <<jump End_Scene>>

<<elseif $stage == 4>> //Игрок вернулся сдавать экзамен
    Длинный: О! Малой! Готов показать, на что способен? 
    Толстый: Б...
    Длинный: Только быстрее. У Толстого от твоей пиццы война в животе.

    -> Я готов сдавать. //Принять
    <<jump FinalExam>>
    -> Мне нужно еще подготовится. //Отказать
    <<jump End_Scene>>
<<endif>>

// Первое знакомство или обычный подход
Длинный: Эй, малой, стопэ. Ты за штукой пришёл?  
Толстый: Б! ТС...

<<if $seen_visits == 1>>
    // первый раз — варианты мягче
    <<set $seen_visits = 2>>
    -> Штукой? О чем ты? //Принять
        <<jump Intro_Subtext>>
    -> Я не ищу ничего... сомнительного. //Отказаться
        Длинный: Сомнительного? Обижаешь, малой. Наша штука — клевенская клевота, а ты...
        Толстый: ПФ.
        -> Не интересует //Откзааться
        Длинный: Ну и вали тогда.
        <<jump End_Scene>>
        -> Что за штука? //Принять
        <<jump Intro_Subtext>>
        -> Кто вы вообще? //Уклониться
        <<jump Intro_Subtext>>
    -> Кто вы такие? //Уклониться
        <<jump Intro_Subtext>>
<<elseif $seen_visits == 2>>
    // вторая и далее встреча до учебы
    -> Опять вы?//Уклониться
        Длинный: Ты нас знаешь? Вишь, Толстый, я говорил, мы — легенды!
        Толстый: Б! Б! 
        ->Ага, легенды. А в чем? //Уклониться
        <<jump Intro_Subtext>>
        -> Пойду я... //Отказаться
        <<jump End_Scene>>
        -> Я слышал у вас есть... Штука.
            Длинный: Обижаешь! 
        <<jump Intro_Subtext>>
    -> Говорите быстро — я тороплюсь. //Принять
        <<jump Intro_Subtext>>
<<endif>>

=== 
title: Intro_Subtext
---
Длинный: Слухай, мы не абы какие чайники, сечешь? Мы не торгуем всяким... подозрительным, понял?
Толстый: Б! Б-Б! ПФ.
Длинный: Толстый дело говорит! Мы преподаватели, на. Ток не по книжкам, а по звуку. Звук — штука. Понял?
Толстый: Б! ТС.
<<set $seen_visits = 3>>

-> То есть вы продаёте уроки? //Принять
    Длинный: Продаём? Неее. Меняем: дела-помощь — на умение.  
    Толстый: Б!
    <<jump Intro_Offer>>

-> Вы точно не те, за кого себя выдаёте... //Уклониться
    Длинный: Зато мы точно те, кто офигенски звучит. Смотри — один трюк и всё поймешь.
    <<jump Demonstration>>

-> Ага, расскажешь. Пойду я, пожалуй. 
    <<jump Reject_All>>

-> У меня есть кое-что, может, подойдет.//Принять, скип если есть предметы
    <<if $has_looper or $has_camera or $has_pizza>>
        Длинный: О, так ты подготовился? Ща посмотрим, что там у тебя.  
        <<jump Inventory_Check>>
    <<else>>
        Длинный: Не, тут ниче интересного. Ладно, идём по обычке. 
        <<jump Intro_Offer>>
    <<endif>>

=== 
title: Demonstration
---
// Тут должна быть полноценная демонстрация, а не просто "базовая бочка"
Длинный: Б! ПФ-ПФ-ПФ. ТС... ТС... Б! Б-Б!  ПФ-ПФ.
Толстый: ТС-ТС-ТС-ТС... Б! ПФ. Б! ПФ. Б! ПФ. ТС...
Длинный: Слухай — сделаем так: у нас три дела, ты делаешь — мы учим. Понял?

-> Я в деле.
    <<jump Intro_Offer>>
-> Не интересует. 
    <<jump Reject_All>>

=== 
title: Inventory_Check
---
<<if $has_looper and $stage == 1>>
    Длинный: О, у тебя есть лупер. Хмм… сломан или рабочий?
    -> Рабочий. 
        <<set $looper_given = true>>
        <<set $stage = 2>>
        Длинный: Ладно, красавчик — ставь его тут. Готов учиться, поди?
        Толстый: Б! ТС-ТС...
        // Тут выбор, получить урок сейчас или позже
        -> Да
        <<jump Give_Lesson>>
        -> Нет, я вернусь позже
        <<detour WalkAwayPrompt>>
        <<jump Give_Lesson>>
    -> Сломан.
        Длинный: Ну хоть честно. Но пока не починишь никакой тебе штуки. 
        Толстый: ПФ-ПФ.
        <<jump End_Scene>>
<<elseif $has_camera and $stage == 2>>
    Длинный: Камера есть? Ща посмотрим.
    -> Да, держи
        <<set $camera_given = true>>
        <<set $stage = 3>>
        Длинный: Отлично. Погнали учить вторую штуку.
        // Тут выбор, получить урок сейчас или позже
        -> Я готов
        <<jump Give_Lesson>>
        -> Я сейчас не могу. 
        <<detour WalkAwayPrompt>>
        <<jump Give_Lesson>>
<<elseif $has_pizza and $stage == 3>>
    Длинный: Пицца у тебя? Смотри, нам нужна именно какая-то шутка во вкусе.
    -> Да, принес. 
        <<set $pizza_given = true>>
        <<set $stage = 4>>
        Длинный: Сюда! Че, готов к последнему уроку, получается?
        // Тут выбор, получить урок сейчас или позже
        -> Да
        <<jump Give_Lesson>>
        -> Нет, я вернусь позже
        <<detour WalkAwayPrompt>>
        <<jump Give_Lesson>>
<<else>>
    Длинный: Погодь, малой — не всё сразу. Мы по этапам работаем. Ты нам так весь флоу порушишь, сечешь?
    <<jump Intro_Offer>>
<<endif>>

=== 
title: Intro_Offer
---
Длинный: Слухай сюда, схема простая. Сначала поможешь нам с лупером, вернёшься — мы научим тебя одному трюку. 
Толстый: Б!
Длинный: Потом — камеру найдешь, потом — добудешь че-нить перекусить. Всё по ступеням, как в школе, но круче!
Толстый: Б-Б! ПФ.
Длинный: Ну че?
<<set $stage = 1>>

-> Я сделаю, что смогу. //Принять
    <<jump LooperQuest>>
->  Я не хочу ввязываться // Отвергнуть
    <<jump Reject_All>>

=== 
title: WalkAwayPrompt
---
Длинный: Малой, помни — мы не раздаём мастер-классы просто так. Хочешь урок — принеси нужную штуку. Бит не ждет!  
-> Я все равно пожалуй пойду
    Длинный: Договорились. Увидимся.
    <<jump End_Scene>>
-> Нет, я передумал.
    Длинный: Ну погнали тогда. Че мы там делалали говоришь?
    <<return>>

=== 
title: LooperQuest
---
<<if $looper_given>>
    Длинный: Ты уже принес лупер — молодец. Готов учиться?
    <<jump Give_Lesson>>
<<else>>
    Длинный: Значит так. Лупстанция для батла исправная нужна — и ты идёшь ее чинить. 
    Толстый: Б! ТС...
    Длинный: Наш парень, Кислый, знает, че делать. Он в подвале, где «Все для дома» ошивается.

    -> Окей. Разберусь.  //Принять
        // миссия выполняется вне Yarn — предполагаем успешный результат
        <<set $looper_given = true>>
        <<set $stage = 2>>
        Длинный: Только чтоб как новенький, понял? Как принесёшь — научим бочке.  
        <<jump End_Scene>>
    -> Я найду замену //Уклониться
        Длинный: Замена? Если есть что-то равноценное — примем.
             -> Могу дать *любой предмет из инвентаря*? //Уклониться
                 Длинный: Нет, так не пойдет. Вот, лупер-ка почини и харе с тебя.
                 // миссия выполняется вне Yarn — предполагаем успешный результат
                  <<set $looper_given = true>>
                  <<set $stage = 2>>
                  <<jump End_Scene>> 
            -> Могу заплатить деньгами //Уклониться
                Длинный: Деньги — не наш метод. Иди лучше лупер чинить, малой.
                // миссия выполняется вне Yarn — предполагаем успешный результат
                 <<set $looper_given = true>>
                 <<set $stage = 2>>
                 <<jump End_Scene>> 
    -> Мне это не интересно //Отказаться
        Длинный: Тогда тебе и уроки не светят, малой.
        <<jump Reject_All>>
<<endif>>

=== 
title: Give_Lesson
---
// Выдача урока — только когда stage соответствует
<<if $stage == 2 and $looper_given and $beat_parts < 1>>
    Длинный: Слухай — базовая «бочка». 
    Толстый: Б! Б! Б!
    Длинный: Типа «бум», понял? Но запомни: не спеши — чувствуй живот, выталкивай звук.
    -> Б!
        <<set $beat_parts = $beat_parts + 1>>
        Длинный: Слышу живот. Неплохо. Двигаем дальше.
        Толстый: *одобрительный бит*
        <<jump CameraQuest>>
    -> Б.
        Длинный: Слабенько. Но и ты не такой мастер как мы. Возвращайся, когда будешь готов. Ток не тяни!
        <<jump End_Scene>>
    -> Б...
        Длинный: Мда. Практикуйся, малой. Возвращайся, когда будешь готов. Ток не тяни!
        <<jump End_Scene>>
<<elseif $stage == 3 and $camera_given and $beat_parts == 1>>
    Длинный: Окей — пуф-снер. Попробуй «ПФ». Коротко, резко, четко.
    Толстый: ПФ. ПФ-ПФ. ПФ.
    -> ПФ.
        <<set $beat_parts = $beat_parts + 1>>
        Длинный: Е! Ты молоток. Двигаем дальше.
        <<jump PizzaQuest>>
    -> ПФ...
        Длинный: Треняй дальше. Выдыхать надо... Когда-нить получится.
        <<jump End_Scene>>
    -> ПФ!
        Длинный: Треняй, малой. Мягче надо, сечешь?
        <<jump End_Scene>>
<<elseif $stage == 4 and $pizza_given and $beat_parts == 2>>
    Длинный: Хай-хет — «ТС...». Лёгко и резво. Попробуй «ТС...».
    Толстый: ТС... ТС-ТС-ТС...
    -> ТС...
        <<set $beat_parts = $beat_parts + 1 >>
        Длинный: Всё. Теперь ты знаешь все, что надо.
        <<jump FinalExamOffer>>
    -> ТС!
        Длинный: Ну-ка давай-ка полегче. Работать и работать...
        <<jump End_Scene>>
    -> ТС.
        Длинный: Легче, малой, легче! Придешь снова, когда натренишь звук.
        <<jump End_Scene>>
<<else>>
    // Если игрок вернулся не в тот момент или условия не выполнены
    Длинный: Чёт ты не в теме. Или ты пришёл рано, или чё-то не принёс. Давай-ка делай по порядку.
    <<jump Intro_Offer>>
<<endif>>

=== 
title: CameraQuest
---
<<if $stage < 2>>
    Длинный: Сначала лупер. Не выпендривайся.
    <<jump LooperQuest>>
<<endif>>

<<if $camera_given>>
    Длинный: А вот и камера. Готов учиться, малой?
    <<jump Give_Lesson>>
<<else>>
    Длинный: За камерой сгонять надо одному челу на районе. За Пыхом давно должок, так что он нам свою камеру и... одолжит.
    Толстый: ПФ-ПФ.
    -> Окей, я схожу//Принять 
        <<set $camera_given = true>>
        <<set $stage = 3>>
        Длинный: Айда. Вернёшься — научим тебя пуф-снейру.
        <<jump End_Scene>>
    -> Где его найти? //Уклониться
        Длинный: Да хер его знает. Ныкается он, поищи за гаражами. Пых тот еще паровоз, на.
        Толстый: ПФ. ТС...
        <<jump End_Scene>>
    -> Я не буду этим заниматься. //Отказ
        <<jump Reject_All>>
<<endif>>

=== title: PizzaQuest
---
<<if $stage < 3>>
    Длинный: Сначала камера. Не шелуди.
    <<jump CameraQuest>>
<<endif>>

<<if $pizza_given>>
    Длинный: О! Хавка! Давай-ка сюда... Чё смотришь?
    Толстый: Б! Б-Б-Б!
    Длинный: А. Урок, точняк. Ну, давай, только по-быстрому.
    <<jump Give_Lesson>>
<<else>>
    Длинный: Ну смари. Последнее, так сказать испытание.
    Толстый: Б! Б-Б! ТС...
    Длинный: Нужна пицца с приколом — не просто ананас, а что-то, чтобы мы ах...нули. Ага.
    -> Понял. Найду//Принять
        // миссия выполняется вне Yarn — предполагаем успешный результат 
        <<set $pizza_given = true >>
        <<set $stage = 4>>
        Длинный: Отлично. Только быстрее, понял?
        <<jump End_Scene>>
    -> С приколом?
        Длинный: Кинь туда что-то нестандартное. Творцам нужны эксперименты, на.
        <<jump End_Scene>>
    -> У меня нет на это времени //Отказ
        <<jump Reject_All>>
<<endif>>

=== title: Reject_All
---
Длинный: Ладно, если ты трус — не обижаемся. Район большой, желающих найдем. А ты вали тады.
Толстый: ПФ-ПФ. 
<<jump End_Scene>>

=== 
title: FinalExamOffer
---
<<if $stage == 4 and $beat_parts == 3>>
    Длинный: Опа! Ты закончил, малой. Теперь — экзамен. Сможешь повторить связку за нами с Толстым — станешь почтенным членом бит-банды, на.
    Толстый: Б! ТС...
    Длинный: Ну че ты? Начинаем?
    -> Да
        <<jump FinalExam>>
    -> Я не готов 
        Длинный: Твое право. Только тренируйся получше. Провалишься — на районе засмеют.
        <<jump End_Scene>>
<<else>>
    Длинный: Ты ещё не готов. Работай по порядку.
    <<jump Intro_Offer>>
<<endif>>

=== 
title: FinalExam
---
// переписать, так чтобы игрок повторял бит за Толстым и Длинным
<<if $beat_parts == 3>>
Длинный: Тебе надо повторить простенькую связку, сечешь? Толстый, задай бит.
Толстый: Б! ТС... ПФ. Б! ТС... ПФ.
Длинный: ПФ-ПФ-ПФ. ТС... ПФ-ПФ-ПФ. ТС... 
Толстый: Б! Б! ТС-ТС... Б! Б! ТССС...

    -> Б! ПФ. ТС...
        Длинный: Эх, школота. Научишься — возвращайся.
        <<jump End_Scene>>

    -> ПФ. Б! ТС...
        Длинный: Эх, школота. Научишься — возвращайся.
        <<jump End_Scene>>

    -> Б! ТС... ПФ.
        Длинный: Е! 
         ->Б! ПФ. ТС...
         Длинный: Эх, школота. Научишься — возвращайся.
         <<jump End_Scene>>

         ->Б! ТС... ПФ.
         Длинный: Ага! Давай еще!
            -> ПФ-ПФ-ПФ. ТС...
            Длинный: Ууух! 
            -> ПФ. ТС... ПФ-ПФ.
            Длинный: Эх, школота. Научишься — возвращайся.
            <<jump End_Scene>>

            -> ПФ-ПФ. ТС...
            Длинный: Ага-ага!
                ->ПФ. ТС... ПФ-ПФ.
                Длинный: Эх, школота. Научишься — возвращайся.
                <<jump End_Scene>>

                ->ПФ-ПФ. ТС...
                Длинный: Эх, школота. Научишься — возвращайся.
                <<jump End_Scene>>

                ->ПФ-ПФ-ПФ. ТС...
                    Длинный: Сочно! 
                    -> Б! Б. ТС-ТС...
                    Длинный: Эх, школота. Научишься — возвращайся.
                    <<jump End_Scene>>
                    -> Б! Б! ТС-ТС...
                    Длинный: Еще чутка... И....!
                        -> Б! ПФ! ТССС...
                        Длинный: Эх, школота. Научишься — возвращайся.
                        <<jump End_Scene>>
                        -> Б! Б! ТССС...
                        Длинный: Есть! Движение, дыхание, характер! Ты сдал, малой!
                        Толстый: *хлоп-хлоп-хлоп*
                        <<set $stage = 5>>
                        -> И что теперь?//Принять
                            Длинный: Ну... Не знаю? Практикуйся, наверное. А мы заявку на батл записывать погнали.
                            <<jump End_Scene>>
                        -> Теперь я могу подаваться на батл с вами?//Уклонится
                            Длинный: Ну-ну, до реальных битв тебе далеко, сечешь? 
                            Толстый:*согласный бит*
                            Длинный: Это мы уже легенды, а тебе еще учиться и учиться. Удачи, кста!
                            <<jump End_Scene>>
                        
                        -> Б! Б. ТССС...
                        Длинный: Эх, школота. Научишься — возвращайся.
                        <<jump End_Scene>>
    
                    -> Б! Б! ТС-ТС!
                    Длинный: Эх, школота. Научишься — возвращайся.
                    <<jump End_Scene>>

         ->ПФ. Б! ТС... 
         Длинный: Эх, ребёнок. Научишься — возвращайся.
        <<jump End_Scene>>
        
<<endif>>
=== 
title: End_Scene
---
*Выходим из диалога. NPC уходят по своим делам или остаются, в зависимости от стадии.*

-> Подойти повторно
    <<jump Approach_Stall>>

-> Закончить демо диалога
    <<End>>

<<End>>
===